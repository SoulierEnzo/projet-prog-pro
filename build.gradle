plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'checkstyle'
	id 'jacoco'
	id 'com.github.spotbugs' version '6.0.18'
}

group = 'ace'
version = '0.0.1-SNAPSHOT'
description = 'job-interview-simulator'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.13.0'

	implementation 'org.springframework.boot:spring-boot-starter'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

checkstyle {
	toolVersion = "10.18.1"
}

spotbugs {
	toolVersion = '4.9.7'
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask).configureEach {
	jvmArgs.set(['-Duser.language=en', '-Duser.country=US'])

	reports {
		html.required.set(true)
		html.outputLocation.set(file("$buildDir/reports/spotbugs/${name}.html"))
		xml.required.set(false)
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

jacocoTestReport {
	dependsOn test

	reports {
		xml.required.set(true)
		csv.required.set(false)
		html.required.set(true)
	}
}

check.dependsOn jacocoTestReport

